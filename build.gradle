buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'net.buycraft' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'buycraftx-forge'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenLocal()
}

dependencies {
    shadow 'net.buycraft:buycraftx-plugin-shared:12.0.1'
}

version = configurations.getByName("shadow").dependencies.find().version // copy common version

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'okhttp3', 'net.buycraft.plugin.internal.okhttp3'
    relocate 'okio', 'net.buycraft.plugin.internal.okio'
    relocate 'retrofit2', 'net.buycraft.plugin.internal.retrofit2'
    relocate 'com.fasterxml.jackson', 'net.buycraft.plugin.internal.jackson'

    classifier '' // Replace the default JAR
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
